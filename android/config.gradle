/*
 *   Copyright Â© 2018-2019 PSPDFKit GmbH. All rights reserved.
 *
 *   THIS SOURCE CODE AND ANY ACCOMPANYING DOCUMENTATION ARE PROTECTED BY INTERNATIONAL COPYRIGHT LAW
 *   AND MAY NOT BE RESOLD OR REDISTRIBUTED. USAGE IS BOUND TO THE PSPDFKIT LICENSE AGREEMENT.
 *   UNAUTHORIZED REPRODUCTION OR DISTRIBUTION IS SUBJECT TO CIVIL AND CRIMINAL PENALTIES.
 *   This notice may not be removed from this file.
 */
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

ext.flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

ext.pspdfkitMavenUrl = localProperties.getProperty('pspdfkit.mavenUrl')
if (pspdfkitMavenUrl == null || pspdfkitMavenUrl == '') {
    ext.pspdfkitMavenUrl = 'https://customers.pspdfkit.com/maven/'
}

ext.pspdfkitVersion = localProperties.getProperty('pspdfkit.version')
if (pspdfkitVersion == null || pspdfkitVersion == '') {
    ext.pspdfkitVersion = '6.5.0'
}

ext.pspdfkitMavenModuleName = 'pspdfkit'

ext.pspdfkitFlutterVersion = '1.9.0-SNAPSHOT'

ext.androidCompileSdkVersion = 29
ext.androidBuildToolsVersion = '29.0.1'
ext.androidMinSdkVersion = 19
ext.androidTargetSdkVersion = 29
ext.androidGradlePluginVersion = '3.6.3'

// Returns true when version1 is more recent than or equals to version2.
boolean isMoreRecentOrEqual(String version1, String version2) {
    [version1,version2]*.tokenize('.')*.collect { it as int }.with { u, v ->
       Integer result = [u,v].transpose().findResult{ x,y -> x <=> y ?: null } ?: u.size() <=> v.size()
       return (result == 1 || result == 0)
    } 
}

// Starting from version 5.5.0 we moved to a simplified model
// that uses unified binaries when on trial and in production
// https://pspdfkit.com/blog/2019/pspdfkit-android-5-5/#simplified-integration
boolean useUnifiedBinaries(String version) {
String versionUnified = '5.5.0'
return isMoreRecentOrEqual(version, versionUnified)
}
